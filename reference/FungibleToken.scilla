scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import BoolUtils ListUtils IntUtils
library FungibleToken

let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

let two_msgs =
fun (msg1 : Message) =>
fun (msg2 : Message) =>
  let msgs_tmp = one_msg msg2 in
  Cons {Message} msg1 msgs_tmp

(* Error events *)
type Error =
  | CodeNotAuthorized
  | CodeNotFound
  | CodeInsufficientFunds

let make_error_event =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeNotAuthorized     => Int32 -1
      | CodeNotFound          => Int32 -2
      | CodeInsufficientFunds => Int32 -3
      end
    in
    { _eventname : "Error"; code : result_code }

let min_int =
  fun (a : Uint128) => fun (b : Uint128) =>
  let min = builtin lt a b in
  match min with
  | True =>
    a
  | False =>
    b
  end
    
(* A util function to test equality *)
let f_eq =
  fun (a : ByStr20) =>
  fun (b : ByStr20) =>
    builtin eq a b

(* Instantiate a type function to test membership in a list *)
let is_default_operator = @list_mem ByStr20

(* A util function to get balance of token_owner *)
let get_balance =
  fun (some_bal: Option Uint128) =>
  match some_bal with
  | Some bal => bal
  | None => Uint128 0
  end

(* Dummy user-defined ADT *)
type Unit =
| Unit

let zero = Uint128 0
let one = Uint128 1

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract FungibleToken
(contract_owner: ByStr20,
  name : String,
  symbol: String,
  decimals: Uint32,
  default_operators : List ByStr20,
  init_supply : Uint128
)

(* Mutable fields *)

field total_supply : Uint128 = init_supply

field balances_map: Map ByStr20 Uint128 
  = let emp_map = Emp ByStr20 Uint128 in
  builtin put emp_map contract_owner init_supply

field operators_map: Map ByStr20 (Map ByStr20 Unit) 
  = Emp ByStr20 (Map ByStr20 Unit)

field revoked_default_operators : Map ByStr20 (Map ByStr20 Unit) 
  = Emp ByStr20 (Map ByStr20 Unit)

field allowances_map: Map ByStr20 (Map ByStr20 Uint128) 
  = Emp ByStr20 (Map ByStr20 Uint128)

(**************************************)
(*             Procedures             *)
(**************************************)

(* Emit Errors *)
procedure IssueErrorEvent(err : Error)
  e = make_error_event err;
  event e
end

(* Mint Tokens *)
procedure AuthorizedMint(recipient: ByStr20, amount: Uint128) 
  get_bal <- balances_map[recipient];
  balance = get_balance get_bal;
  new_balance = builtin add amount balance;
  balances_map[recipient] := new_balance;
  current_total_supply <- total_supply;
  new_total_supply = builtin add current_total_supply amount;
  total_supply := new_total_supply;
  e = {_eventname: "Minted"; minter: _sender; recipient: recipient; amount: amount};
  event e
end

(* Burn Tokens *)
procedure AuthorizedBurnIfSufficientBalance(from: ByStr20, amount: Uint128)
  get_bal <- balances_map[from];
  match get_bal with
  | None => 
    err = CodeInsufficientFunds;
    IssueErrorEvent err;
    throw
  | Some bal =>
    can_burn = uint128_le amount bal;
    match can_burn with
    | False =>
      err = CodeInsufficientFunds;
      IssueErrorEvent err;
      throw
    | True =>
      (* Subtract amount from 'from' *)
      new_balance = builtin sub bal amount;
      balances_map[from] := new_balance;
      current_total_supply <- total_supply;
      new_total_supply = builtin sub current_total_supply amount;
      total_supply := new_total_supply;
      e = {_eventname: "Burnt"; burner: _sender; burn_account: from; amount: amount};
      event e  
      end
   end
end

(* Move Tokens if authorized *)
procedure AuthorizedMoveIfSufficientBalance(from: ByStr20, to: ByStr20, amount: Uint128)
  get_from_bal <- balances_map[from];
  match get_from_bal with
  | Some bal =>
    can_do = uint128_le amount bal;
    match can_do with
    | True =>
      (* Subtract amount from "from" and add it to "to" address *)
      new_from_bal = builtin sub bal amount;
      balances_map[from] := new_from_bal;
      (* Adds amount to "to" address *)
      get_to_bal <- balances_map[to];
      new_to_bal = match get_to_bal with
      | Some bal => builtin add bal amount
      | None => amount
      end;
      balances_map[to] := new_to_bal
    | False =>
      (* Balance not sufficient *)
      err = CodeInsufficientFunds;
      IssueErrorEvent err;
      throw
    end
  | None =>
    err = CodeNotFound;
    IssueErrorEvent err;
    throw
  end
end

(***************************************)
(*             Transitions             *)
(***************************************)

(* Getter transitions *)

(* @dev: Check if an address is an operator or default operator of a token_owner. Provide a Bool *)
(* @param operator:    Address of a potential operator.                                          *)
(* @param token_owner: Address of a token_owner.                                                 *)
transition IsOperatorFor(token_owner: ByStr20, operator: ByStr20)
  is_operator_approved <- exists operators_map[token_owner][operator];
  is_default_operator = is_default_operator f_eq operator default_operators;
  is_revoked_operator <- exists revoked_default_operators[token_owner][operator];
  is_default_operator_approved = 
    let is_not_revoked_operator = negb is_revoked_operator in 
    andb is_not_revoked_operator is_default_operator;
  is_approved = orb is_operator_approved is_default_operator_approved;
  msg_to_sender = { _tag : "IsOperatorForCallBack"; _recipient : _sender; _amount : Uint128 0;
                    is_operator_for : is_approved};
  msgs = one_msg msg_to_sender;
  send msgs
end

(* Optional transitions *)

(* @dev: Optional transition. Mint new tokens. Only contract_owner can mint. *)
(* @param recipient: Address of the recipient whose balance is to increase.  *)
(* @param amount:    Number of tokens to be minted.                          *)
transition Mint(recipient: ByStr20, amount: Uint128)
  is_owner = builtin eq _sender contract_owner;
   match is_owner with
    | False =>
      err = CodeNotAuthorized;
      IssueErrorEvent err;
      throw
    | True =>
      AuthorizedMint recipient amount;
      (* Prevent sending to a contract address that does not support transfers of token *)
      msg_to_recipient = {_tag : "RecipientAcceptMint"; _recipient : recipient; _amount : zero; 
                          minter : _sender; recipient : recipient; amount : amount};
      msg_to_sender = {_tag : "MintSuccessCallBack"; _recipient : _sender; _amount : zero; 
                          minter : _sender; recipient : recipient; amount : amount};
      msgs = two_msgs msg_to_recipient msg_to_sender;
      send msgs
    end
end

(* @dev: Optional transition. Burn existing tokens. Only contract_owner can burn. *)
(* @param burn_account: Address of the token_owner whose balance is to decrease.  *)
(* @param amount:       Number of tokens to be burned.                            *)
transition Burn(burn_account: ByStr20, amount: Uint128)
  is_owner = builtin eq _sender contract_owner;
    match is_owner with
    | False =>
      err = CodeNotAuthorized;
      IssueErrorEvent err;
      throw
    | True =>
      AuthorizedBurnIfSufficientBalance burn_account amount;
      msg_to_sender = {_tag : "BurnSuccessCallBack"; _recipient : _sender; _amount : zero; 
                        burner : _sender; burn_account : burn_account; amount : amount};
      msgs = one_msg msg_to_sender;
      send msgs
    end
end

(* Compulsory interface transitions *)

(* @dev: Make an address an operator of the caller.             *)
(* @param operator: Address to be authorize as operator or      *)
(* Re-authorize as default_operator. Cannot be calling address. *)
transition AuthorizeOperator(operator: ByStr20)
  is_sender = builtin eq operator _sender;
  match is_sender with
  | True => 
    (* _sender is authorizing self as operator, return error code *)
    err = CodeNotAuthorized;
    IssueErrorEvent err
  | False =>
    is_default_operator =  is_default_operator f_eq operator default_operators;
    match is_default_operator with
    | True =>
      (* Re-authorize default_operator *)
      delete revoked_default_operators[_sender][operator];
      e = { _eventname : "ReAuthorizedDefaultOperatorSuccess"; authorizer : _sender; reauthorized_default_operator : operator};
      event e
    | False =>
      (* Authorize new operator *)
      authorize = Unit;
      operators_map[_sender][operator] := authorize;
      e = {_eventname : "AuthorizeOperatorSuccess"; authorizer : _sender; authorized_operator : operator};
      event e
    end
  end
end

(* @dev: Revoke an address from being an operator or default_operator of the caller. *)
(* @param operator: Address to be removed as operator or default_operator.           *)
transition RevokeOperator(operator: ByStr20)
  is_default_operator = is_default_operator f_eq operator default_operators;
  match is_default_operator with
  | False =>
    (* Not default_operator, check if operator *)
    get_operator <- operators_map[_sender][operator];
    match get_operator with
    | None =>
      (* Operator to be removed not found, error *)
      err = CodeNotFound;
      IssueErrorEvent err
    | Some status =>
      delete operators_map[_sender][operator];
      e = {_eventname : "RevokeOperatorSuccess"; revoker : _sender; revoked_operator : operator};
      event e
    end
  | True =>
    (* Is default_operator, revoke default_operator authority instead *)
    verdad = Unit;
    revoked_default_operators[_sender][operator] := verdad;
    e = {_eventname : "RevokedDefaultOperatorSuccess"; revoker : _sender; revoked_default_operator : operator};
    event e
  end
end

(* @dev: Increase the allowance of an approved_spender over the caller’s tokens. Only token_owner allowed to invoke. *)
(* param spender:      Address of the designated approved_spender.                                                   *)
(* param amount:       Number of tokens to be increased as allowance for the approved_spender.                       *)
transition IncreaseAllowance(spender: ByStr20, amount: Uint128)
  (* Checks if the _sender and approved_spender is the same *)
  is_owner = builtin eq _sender spender;
  match is_owner with
  | True =>
    err = CodeNotAuthorized;
    IssueErrorEvent err
  | False =>
    get_current_allowance <- allowances_map[_sender][spender];
    current_allowance =
      match get_current_allowance with
      | Some allowance => allowance
      | None => zero
      end;
    new_allowance = builtin add current_allowance amount;
    allowances_map[_sender][spender] := new_allowance;
    e = {_eventname : "IncreasedAllowance"; token_owner : _sender; spender: spender; new_allowance : new_allowance};
    event e
  end
end

(* @dev: Decrease the allowance of an approved_spender over the caller’s tokens. Only token_owner allowed to invoke. *)
(* param spender:      Address of the designated approved_spender.                                                   *)
(* param amount:       Number of tokens to be decreased as allowance for the approved_spender.                       *)
transition DecreaseAllowance(spender: ByStr20, amount: Uint128)
  (* Checks if the _sender and approved_spender is the same *)
  is_owner = builtin eq _sender spender;
  match is_owner with
  | True =>
    err = CodeNotAuthorized;
    IssueErrorEvent err
  | False =>
    get_current_allowance <- allowances_map[_sender][spender];
    current_allowance =
      match get_current_allowance with
      | Some allowance => allowance
      | None => zero
      end;
    new_allowance =
      let amount_le_allowance = uint128_le amount current_allowance in
      match amount_le_allowance with
      | True => builtin sub current_allowance amount
      | False => zero
      end;
    allowances_map[_sender][spender] := new_allowance;
    e = {_eventname : "DecreasedAllowance"; token_owner : _sender; spender: spender; new_allowance : new_allowance};
    event e
  end
end

(* @dev: Moves an amount tokens from _sender to the recipient. Used by token_owner. *)
(* @dev: Balance of recipient will increase. Balance of _sender will decrease.      *)
(* @param to:  Address of the recipient whose balance is increased.                 *)
(* @param amount:     Amount of tokens to be sent.                                  *)
transition Transfer(to: ByStr20, amount: Uint128)
  AuthorizedMoveIfSufficientBalance _sender to amount;
  e = {_eventname : "Transfer"; sender : _sender; recipient : to; amount : amount};
  event e;
  (* Prevent sending to a contract address that does not support transfers of token *)
  msg_to_recipient = {_tag : "RecipientAcceptTransfer"; _recipient : to; _amount : zero; 
                      sender : _sender; recipient : to; amount : amount};
  msg_to_sender = {_tag : "TransferSuccessCallBack"; _recipient : _sender; _amount : zero; 
                  sender : _sender; recipient : to; amount : amount};
  msgs = two_msgs msg_to_recipient msg_to_sender;
  send msgs
end

(* @dev: Moves amount tokens from token_owner to recipient. _sender must be an operator of token_owner. *)
(* @dev: Balance of recipient will increase. Balance of token_owner will decrease.                      *)
(* @param from:        Address of the token_owner whose balance is decreased.                           *)
(* @param to:          Address of the recipient whose balance is increased.                             *)
(* @param amount:      Amount of tokens to be sent.                                                     *)
transition OperatorSend(from: ByStr20, to: ByStr20, amount: Uint128)
  is_operator_approved <- exists operators_map[from][_sender];
  is_default_operator = is_default_operator f_eq _sender default_operators;
  is_revoked_operator <- exists revoked_default_operators[from][_sender];
  is_default_operator_approved = let is_not_revoked_operator = 
    negb is_revoked_operator in andb is_not_revoked_operator is_default_operator;
  is_approved = orb is_operator_approved is_default_operator_approved;
  match is_approved with
    | False =>
      err = CodeNotAuthorized;
      IssueErrorEvent err;
      throw
    | True =>
      AuthorizedMoveIfSufficientBalance from to amount;
      e = {_eventname : "OperatorSendSuccess"; initiator : _sender; sender : from; recipient : to; amount : amount};
      event e;
      (* Prevent sending to a contract address that does not support transfers of token *)
      msg_to_recipient = {_tag : "RecipientAcceptOperatorSend"; _recipient : to; _amount : zero; 
                          initiator : _sender; sender : from; recipient : to; amount : amount};
      msg_to_sender = {_tag : "OperatorSendSuccessCallBack"; _recipient : _sender; _amount : zero; 
                      initiator : _sender; sender : from; recipient : to; amount : amount};
      msgs = two_msgs msg_to_recipient msg_to_sender;
      send msgs
   end
end

(* @dev: Move a given amount of tokens from one address to another using the allowance mechanism. The caller must be an approved_spender. *)
(* @dev: Balance of recipient will increase. Balance of token_owner will decrease.                                                        *)
(* @param from:    Address of the token_owner whose balance is decreased.                                                                 *)
(* @param to:      Address of the recipient whose balance is increased.                                                                   *)
(* @param amount:  Amount of tokens to be transferred.                                                                                    *)
transition TransferFrom(from: ByStr20, to: ByStr20, amount: Uint128)
  get_bal <- balances_map[from];
  match get_bal with
  | None => 
    err = CodeNotFound;
    IssueErrorEvent err;
    throw
  | Some bal =>
    get_spender_allowed <- allowances_map[from][_sender];
    match get_spender_allowed with
    | None =>
      err = CodeNotAuthorized;
      IssueErrorEvent err;
      throw
    | Some allowed =>
      min = min_int bal allowed;
      can_do = uint128_le amount min;
      match can_do with
      | True =>
        AuthorizedMoveIfSufficientBalance from to amount;
        e = {_eventname : "TransferFromSuccess"; initiator : _sender; sender : from; recipient : to; amount : amount};
        event e;
        new_allowed = builtin sub allowed amount;
        allowances_map[from][_sender] := new_allowed;
        (* Prevent sending to a contract address that does not support transfers of token *)
        msg_to_recipient = {_tag : "RecipientAcceptTransferFrom"; _recipient : to; _amount : zero; 
                            initiator : _sender; sender : from; recipient : to; amount : amount};
        msg_to_sender = {_tag : "TransferFromSuccessCallBack"; _recipient : _sender; _amount : zero; 
                        initiator : _sender; sender : from; recipient : to; amount : amount};
        msgs = two_msgs msg_to_recipient msg_to_sender;
        send msgs
      | False =>
        err = CodeInsufficientFunds;
        IssueErrorEvent err;
        throw
      end
    end
  end
end
